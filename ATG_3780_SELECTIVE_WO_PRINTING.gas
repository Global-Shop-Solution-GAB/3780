Program.Sub.ScreenSU.Start
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start

Program.Sub.Preflight.End

Program.Sub.Main.Start
'Coded by: Melinda Keyes
'Project Start Date: 10/4/2011
'Hooks:
'  16090 - BI Pre-Process
'Notes:
'  Module Shop Floor Control > Reports > Bar Code Work Order (Extended):
'  When the user hits the OK button, skip printing the WO with only Material  and Laser sequences.
'  If there are WO with additional sequences, along with Material and Laser, those WO have to be Printed.
'  If there are no Work orders in the selected range with sequences other than the Material and Laser, then cancel the Print.

'  Requires updated BJ020LZG and GHOOK
'  Tables are BI_BAR_HEAD, BI_BAR_DETAIL
'  Report 1811

F.Intrinsic.Control.SetErrorHandler("Main_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
V.Local.sQuery.Declare(String)
V.Local.sJob.Declare(String)
V.Local.sSuffix.Declare(String)
V.Local.sLMO.Declare(String)
V.Local.sWC.Declare(String)
V.Local.sOmit.Declare(String)
V.Local.iRec.Declare(Long)
V.Local.bPrint.Declare(Boolean)

F.Intrinsic.Control.If(V.Caller.Hook,=,16090)
	F.Intrinsic.Control.If(V.Passed.009001,=,"001811")
		F.ODBC.Connection!conX.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)
		F.Intrinsic.String.Concat("SELECT * FROM BI_BAR_HEAD WHERE TERMINAL = '",V.Caller.Terminal,"' ORDER BY KEY_SEQ",V.Local.sQuery)
		F.ODBC.Connection!conX.OpenRecordsetRO("rst",V.Local.sQuery)
		'loop through each work order to be printed
		F.Intrinsic.Control.DoUntil(V.ODBC.conX!rst.EOF,=,True)
			V.Local.bPrint.Set(False)
			V.Local.sJob.Set(V.ODBC.conX!rst.FieldVal!JOB)
			V.Local.sSuffix.Set(V.ODBC.conX!rst.FieldVal!SUFFIX)
			F.Intrinsic.String.Concat("SELECT * FROM BI_BAR_DETAIL WHERE JOB = '",V.Local.sJob.PSQLFriendly,"' AND SUFFIX = '",V.Local.sSuffix.PSQLFriendly,"' AND TERMINAL = '",V.Caller.Terminal,"' ORDER BY KEY_SEQ",V.Local.sQuery)
			F.ODBC.Connection!conX.OpenRecordsetRO("rstDetail",V.Local.sQuery)
			'loop through each sequence of the work order
			F.Intrinsic.Control.DoUntil(V.ODBC.conX!rstDetail.EOF,=,True)
				V.Local.sLMO.Set(V.ODBC.conX!rstDetail.FieldValTrim!LMO)
				V.Local.sWC.Set(V.ODBC.conX!rstDetail.FieldValTrim!WC_NAME)
				'if the work order has any sequences that are NOT material or labor (with workcenter as LASER) print the work order
				F.Intrinsic.Control.If(V.Local.sLMO,<>,"M")
				F.Intrinsic.Control.AndIf(V.Local.sLMO,<>,"C")
					F.Intrinsic.Control.If(V.Local.sLMO,=,"L")
						F.Intrinsic.Control.If(V.Local.sWC,<>,"MITS 4000W '09 5X10")
							V.Local.bPrint.Set(True)
						F.Intrinsic.Control.EndIf
					F.Intrinsic.Control.Else
						V.Local.bPrint.Set(True)
					F.Intrinsic.Control.EndIf
				F.Intrinsic.Control.EndIf
				F.ODBC.conX!rstDetail.MoveNext
			F.Intrinsic.Control.Loop
			F.ODBC.conX!rstDetail.Close
			'if the work order only has lines for material and labor at the LASER workstation, delete the records from the BI tables
			F.Intrinsic.Control.If(V.Local.bPrint,=,False)
				F.Intrinsic.String.Concat("DELETE FROM BI_BAR_HEAD WHERE JOB = '",V.Local.sJob.PSQLFriendly,"' AND SUFFIX = '",V.Local.sSuffix.PSQLFriendly,"' AND TERMINAL = '",V.Caller.Terminal,"'",V.Local.sQuery)
				F.ODBC.Connection!conX.Execute(V.Local.sQuery)
				F.Intrinsic.String.Concat("DELETE FROM BI_BAR_DETAIL WHERE JOB = '",V.Local.sJob.PSQLFriendly,"' AND SUFFIX = '",V.Local.sSuffix.PSQLFriendly,"' AND TERMINAL = '",V.Caller.Terminal,"'",V.Local.sQuery)
				F.ODBC.Connection!conX.Execute(V.Local.sQuery)
				F.Intrinsic.String.Concat(V.Local.sOmit," ",V.Local.sJob,"-",V.Local.sSuffix,V.Local.sOmit)
			F.Intrinsic.Control.EndIf
			F.ODBC.conX!rst.MoveNext
		F.Intrinsic.Control.Loop
		F.ODBC.conX!rst.Close

	'notifiy the user if work orders were not printed
	F.Intrinsic.Control.if(V.Local.sOmit,<>,"")
		F.Intrinsic.String.Concat("The following work orders were ommitted:",V.Local.sOmit,V.Local.sOmit)
		F.Intrinsic.UI.Msgbox(V.Local.sOmit)
	F.Intrinsic.Control.EndIf

	'if there are no records left to print, cancel the printing
	F.ODBC.Connection!conX.OpenRecordsetRO("rst","SELECT * FROM BI_BAR_HEAD ORDER BY KEY_SEQ")
	F.Intrinsic.Control.If(V.ODBC.conX!rst.EOF,=,True)
		F.Intrinsic.UI.Msgbox("No work orders were printed.")
		V.Passed.CANCEL.Set(1)
	F.Intrinsic.Control.EndIf
	F.ODBC.conX!rst.Close

	F.ODBC.Connection!conX.Close
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf
F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Main_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_3780_SELECTIVE_WO_PRINTING.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
Function.Intrinsic.Control.EndIf

Program.Sub.Main.End

Program.Sub.Comments.Start
${$0$}$ATG_3780_SELECTIVE_WO_PRINTING$}$JCT$}$4/11/2016
${$1$}$$}$$}$0$}$16090$}$BSI005-PRE-PROCESS-HOOK$}$4/11/2016 3:57:04 PM$}$(Program: BSI005; Screen: )

Program.Sub.Comments.End

